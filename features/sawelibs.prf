SAWEROOT = $$system(echo $$_PRO_FILE_PWD_ | perl -pe \'s|(.*?sonicawe)/.*|\\1|\')

QT += opengl
QT += network

INCLUDEPATH += \
    $$SAWEROOT/lib/gpumisc \
    $$SAWEROOT/lib/backtrace \
    $$SAWEROOT/lib/justmisc \
    $$SAWEROOT/lib/signal \
    $$SAWEROOT/lib/tfr \
    $$SAWEROOT/lib/heightmap \
    $$SAWEROOT/src \

unix:!macx {
INCLUDEPATH += \
    $$SAWEROOT/lib/sonicawe-ubuntulib/include \

LIBS += \
    -lsndfile \
    -lGLEW \
    -lGLU \
    -lGL \
    -lboost_chrono \
    -lboost_serialization \
    -lboost_system \
    -lboost_thread \
    -lglut \
    -L$$SAWEROOT/lib/sonicawe-ubuntulib/lib \
    -lportaudiocpp -lportaudio \
    -lhdf5 -lhdf5_hl \
    -L$$SAWEROOT/lib/justmisc -ljustmisc \
    -L$$SAWEROOT/lib/backtrace -lbacktrace \
    -L$$SAWEROOT/lib/gpumisc -lgpumisc \
    -L$$SAWEROOT/lib/signal -lsignal \
    -L$$SAWEROOT/lib/tfr -ltfr \
    -L$$SAWEROOT/lib/heightmap -lheightmap \

QMAKE_LFLAGS += -Wl,-rpath=\'\$\${ORIGIN}\'
}

macx {
LIBS += -lsndfile \
    -framework GLUT \
    -framework OpenGL \
    -lportaudiocpp -lportaudio \
    -lhdf5 -lhdf5_hl \
    -lboost_chrono-mt \
    -lboost_serialization-mt \
    -lboost_system-mt \
    -lboost_thread-mt \
    -L$$SAWEROOT/lib/justmisc -ljustmisc \
    -L$$SAWEROOT/lib/backtrace -lbacktrace \
    -L$$SAWEROOT/lib/gpumisc -lgpumisc \
    -L$$SAWEROOT/lib/signal -lsignal \
    -L$$SAWEROOT/lib/tfr -ltfr \
    -L$$SAWEROOT/lib/heightmap -lheightmap \

# macports
exists(/opt/local/include/): INCLUDEPATH += /opt/local/include/
exists(/opt/local/lib): LIBS += -L/opt/local/lib

# homebrew
exists(/usr/local/include/): INCLUDEPATH += /usr/local/include/
exists(/usr/local/lib): LIBS += -L/usr/local/lib
}

win32 {
INCLUDEPATH += \
        $$SAWEROOT/lib/sonicawe-winlib/glut \
        $$SAWEROOT/lib/sonicawe-winlib/glew/include \
        $$SAWEROOT/lib/sonicawe-winlib/portaudio/include \
        $$SAWEROOT/lib/sonicawe-winlib/libsndfile/include \
        $$SAWEROOT/lib/sonicawe-winlib/hdf5lib/include \
        $$SAWEROOT/lib/sonicawe-winlib/zlib/include \
        $$SAWEROOT/lib/sonicawe-winlib/omp \
        $$SAWEROOT/lib/sonicawe-winlib
LIBS += \
        -l$$SAWEROOT/lib/sonicawe-winlib/glut/glut32 \
        -l$$SAWEROOT/lib/sonicawe-winlib/glew/lib/glew32 \
        -l$$SAWEROOT/lib/sonicawe-winlib/libsndfile/libsndfile-1 \
        -l$$SAWEROOT/lib/sonicawe-winlib/hdf5lib/dll/hdf5dll \
        -l$$SAWEROOT/lib/sonicawe-winlib/hdf5lib/dll/hdf5_hldll \
        -L$$SAWEROOT/lib/sonicawe-winlib/boostlib \
        -L$$SAWEROOT/lib/sonicawe-winlib/omp \
        $$SAWEROOT/lib/sonicawe-winlib/portaudio/portaudio.lib \

CONFIG(debug, debug|release): LIBS += \
        $$SAWEROOT/lib/sonicawe-winlib/portaudio/portaudiocpp_mt_gd.lib
else: LIBS += \
        $$SAWEROOT/lib/sonicawe-winlib/portaudio/portaudiocpp_mt.lib

}
